# Description:
#   Returns weather information from Forecast.io with a sprinkling of Google maps.
#
# Configuration:
#   HUBOT_WEATHER_CELSIUS - Display in celsius
#   HUBOT_FORECAST_API_KEY - Forecast.io API Key
#
# Commands:
#   hubot weather <city> - Get the weather for a location.
#   hubot forecast <city> - Get the 3 day forecast for a location.
#
# Author:
#   markstory
#   mbmccormick
env = process.env

forecastIoUrl = 'https://api.forecast.io/forecast/' + process.env.HUBOT_FORECAST_API_KEY + '/'
googleMapUrl = 'http://maps.googleapis.com/maps/api/geocode/json'

lookupAddress = (msg, location, cb) ->
  location = "Melbourne" if location is ["ë©œë²ˆ", "ë©œë²„ë¥¸"]
  location = "Sydney" if location in ["ì‹œë“œë‹ˆ", "ì‹¯ë‹ˆ"]
  msg.http(googleMapUrl).query(address: location, sensor: true)
    .get() (err, res, body) ->
      try
        body = JSON.parse body
        coords = body.results[0].geometry.location
      catch err
        err = "ðŸ¨ #{location}... ì–´ë”˜ì§€ ëª¨ë¥´ê² ì–´ìš”."
        return cb(msg, null, null, err)
      cb(msg, location, coords, err)

lookupWeather = (msg, location, coords, err) ->
  return msg.send err if err
  return msg.send "You need to set env.HUBOT_FORECAST_API_KEY to get weather data" if not env.HUBOT_FORECAST_API_KEY

  url = forecastIoUrl + coords.lat + ',' + coords.lng

  msg.http(url).query(units: 'ca').get() (err, res, body) ->
    return msg.send 'ðŸ¨ ë‚ ì”¨ ì •ë³´ê°€ ì—†ëŠ”ë°ìš”.' if err
    try
      body = JSON.parse body
      current = body.currently
    catch err
      return msg.send "ðŸ¨ ë­ëžœ ê³ ëžŒì‹ ì§€ ëª¨ë¥´ì¿ ë‹¤ì–‘."
    humidity = (current.humidity * 100).toFixed 0
    temperature = getTemp(current.temperature)
    text = "ðŸ¨ #{location}ì˜ í˜„ìž¬ ê¸°ì˜¨ #{temperature} #{current.summary}, ìŠµë„ #{humidity}% ìž…ë‹ˆë‹¹."
    msg.send text

lookupForecast = (msg, location, coords, err) ->
  return msg.send err if err
  return msg.send "You need to set env.HUBOT_FORECAST_API_KEY to get weather data" if not env.HUBOT_FORECAST_API_KEY

  url = forecastIoUrl + coords.lat + ',' + coords.lng
  msg.http(url).query(units: 'ca').get() (err, res, body) ->
    return msg.send 'ðŸ¨ ì¼ê¸°ì˜ˆë³´ ëª¨ë¥´ê² ëŠ”ë°ìš”. ë‰´ìŠ¤ë³´ì„¸ìš”.' if err
    try
      body = JSON.parse body
      forecast = body.daily.data
      today = forecast[0]
      tomorrow = forecast[1]
      dayAfter = forecast[2]
    catch err
      return msg.send 'ðŸ¨ ì¼ê¸°ì˜ˆë³´ ì •ë³´ê°€ ì˜ì–´ë¼ì„œ ì½ì§€ ëª»í•˜ê² ë„¤ìš”.'
    text = "ðŸ¨ #{location}ì˜ ì¼ê¸°ì˜ˆë³´ìž…ë‹ˆë‹¤:\n"

    appendText = (text, data) ->
      dateToday = new Date(data.time * 1000)
      month = dateToday.getMonth() + 1
      day = dateToday.getDate()
      humidity = (data.humidity * 100).toFixed 0
      maxTemp = getTemp data.temperatureMax
      minTemp = getTemp data.temperatureMin

      text += "#{month}/#{day} - ìµœê³  #{maxTemp}, ìµœì € #{minTemp} "
      text += "#{data.summary} ìŠµë„ #{humidity}%\n"
      text

    text = appendText text, today
    text = appendText text, tomorrow
    text = appendText text, dayAfter
    msg.send text

lookupLocation = (msg, location, coords, err) ->
  return msg.send err if err
  msg.send "ðŸ¨ #{location} ìœ„ê²½ë„ëŠ” #{coords.lat}, #{coords.lng} ìž…ë‹ˆë‹¤."
  msg.send "ðŸŒ https://www.google.com/maps/preview/@#{coords.lat},#{coords.lng},8z"

getTemp = (c) ->
  if env.HUBOT_WEATHER_CELSIUS
    return c.toFixed(0) + "ÂºC"
  return ((c * 1.8) + 32).toFixed(0) + "ÂºF"

module.exports = (robot) ->

  robot.respond /weather(?: me|for|in)?\s(.*)/i, (msg) ->
    location = msg.match[1]
    lookupAddress(msg, location, lookupWeather)

  robot.respond /where(?: me|for|in)?\s(.*)/i, (msg) ->
    location = msg.match[1]
    lookupAddress(msg, location, lookupLocation)

  robot.respond /forecast(?: me|for|in)?\s(.*)/i, (msg) ->
    location = msg.match[1]
    lookupAddress(msg, location, lookupForecast)

  robot.respond /(.*)\s(ë‚ ì”¨|ê¸°ìƒ|ê¸°ì˜¨)/i, (msg) ->
    location = msg.match[1]
    lookupAddress(msg, location, lookupWeather)

  robot.respond /(.*)\s(ì–´ë””|ìœ„ì¹˜)/i, (msg) ->
    location = msg.match[1]
    lookupAddress(msg, location, lookupLocation)

  robot.respond /(.*)\s(ì¼ê¸°|ì¼ê¸°ì˜ˆë³´|ê¸°ìƒì²­|ë¹„ì˜¬ë“¯|ëˆˆì˜¬ë“¯)/i, (msg) ->
    location = msg.match[1]
    lookupAddress(msg, location, lookupForecast)
